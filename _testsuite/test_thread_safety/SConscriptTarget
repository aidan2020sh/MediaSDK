####################################################################################
#
# INTEL CORPORATION PROPRIETARY INFORMATION
# This software is supplied under the terms of a license agreement or nondisclosure
# agreement with Intel Corporation and may not be copied or disclosed except in
# accordance with the terms of that agreement
# Copyright(c) 2010-2011 Intel Corporation. All Rights Reserved.
#
####################################################################################

# Importing python-wide modules
import os

# Importing MFX utilities module
import mfx_init as mfx
import mfx_utils

# Importing variables
Import('mfx_src_root')
Import('mfx_tst_lib_env')
Import('mfx_bld_root')
Import('mfx_samples_bld_root')

# Creating build environment
bld_env = mfx_tst_lib_env.Clone()
bld_env['MFX_CURDIR'] = '#_testsuite/test_thread_safety'

# Setting list of additional include directories
includes = [
            '#_studio/mfx_lib/decode/vc1/include',
            '#_testsuite/mfx_player/include',
            '#_testsuite/mfx_transcoder/include',
            '#_testsuite/shared/include',
            '#samples/sample_common/include',
            '#_studio/shared/umc/codec/spl_common/include',
            '#_studio/shared/umc/codec/avi_spl/include',
            '#_studio/shared/umc/codec/demuxer/include',
]
bld_env.Append(CPPPATH = includes)

# Setting additional IPP libraries to link with
libs_ipp = Split('ippvc_l ippcc_l ippdc_l ippi_l ipps_l ippcore_l')
bld_env.Prepend(LIBS = libs_ipp)

# Setting list of additional libraries to link with
libs = ['mfx', 'mfx_trans_pipeline', 'mfx_pipeline', 
        'shared_utils', 'sample_common', 'dispatch_trace',
        'umc_codecs_merged',
        'umc_io_merged_${MFX_IMPL}',
        'umc_core_merged']

libs_path = ['_testsuite/dxva2_dump',
             '_testsuite/shared',
             '_testsuite/mfx_player',
             '_testsuite/mfx_transcoder']
libs_samples_path = ['sample_common']

# Platform specific libs
if bld_env['MFX_PLATFORM'] == 'win':
  libs.append('dxva2_dump')

bld_env.Prepend(LIBS = mfx_utils.prepare_libs(bld_env, libs))
bld_env.Append(LIBPATH = mfx_utils.attach_prefix(mfx_bld_root, libs_path))
bld_env.Append(LIBPATH = mfx_utils.attach_prefix(mfx_samples_bld_root, libs_samples_path))

# Optional packages to compile and link with
if bld_env['MFX_PLATFORM'] == 'lin':
  packages = ['x11', 'libva-x11']
  mfx_utils.check_and_set_packages(bld_env, packages)

# Setting lists of sources
local = mfx_utils.get_filenames(Glob('src/*.cpp'))

# Preparing sources for compilation
sources = mfx_utils.prepare_sources(bld_env, local)
  
# Building application
program = bld_env.Program(target = mfx_utils.get_build_name(bld_env, 'test_thread_safety'), source = sources)

# Installing application
mfx_utils.install_if_exists(bld_env, program)
